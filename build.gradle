buildscript {
    repositories {
        jcenter()
        maven { url = "https://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'

version = "${minecraft_version}-${mod_version}"
group = "epicsquid.mysticalworld"
archivesBaseName = "mysticalworld"

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "1.12.2-14.23.5.2847"
    runDir = "run"

    mappings = "stable_39"

    replace "@VERSION@", project.version
    replaceIn "MysticalWorld.java"
    replace "after:maindependencies", project.depends
    replaceIn "MysticalWorld.java"
}
repositories {
    maven {
        // location of the maven that hosts JEI files
        name = "Progwml6 maven"
        url = "http://dvs1.progwml6.com/files/maven"
    }
    maven {
        // location of a maven mirror for JEI files, as a fallback
        name = "ModMaven"
        url = "modmaven.k-4u.nl"
    }
    maven {
        name = "JER Maven (way2muchnoise)"
        url = "https://dl.bintray.com/way2muchnoise/dev"
    }
    maven {
        name = "MMD Maven"
        url 'https://maven.mcmoddev.com'
    }
    maven {
        //Also contains Patchouli
        name = "Jared Maven"
        url = "https://maven.blamejared.com"
    }
    maven {
        name = 'Tterrag Maven'
        url "http://maven.tterrag.com"
    }
    maven {
        name = 'Curse Maven by WynPrice'
        url = "https://www.cursemaven.com"
    }
    maven {
        url = "https://github.com/TehNut/temporary-maven-thing/tree/master/maven"
    }
}

dependencies {
    if (findProject(':MysticalLib') != null) {
        compile project(':MysticalLib')
    } else {
        deobfCompile(project.dependencies.create("epicsquid.mysticallib:mysticallib:${mysticallib_version}-${mysticallib_build}") {
            transitive = false
        })
    }
    
    deobfCompile("jeresources:JustEnoughResources-${minecraft_version}:${jer_version}") {
        exclude group: 'mezz.jei'
    }
    compile "curse.maven:simpleharvest:2897811"
    compileOnly "vazkii.patchouli:Patchouli:${patchouli_version}"
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
